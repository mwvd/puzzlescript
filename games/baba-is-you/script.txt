title BABA IS YOU demake
author @pyjamads
homepage www.puzzlescript.net
(debug
verbose_logging)
noaction

(zoomscreen 16x16)

========
OBJECTS
========

Background
darkbrown 
00000
00000
00000
00000
00000

Player
transparent

Grass
green
.0..0
.0..0
0.0..
.0..0
0..0.

Tile
darkblue black
..0..
.000.
00000
.000.
..0..

Tree
brown green
.111.
11111
1.0.1
..0..
.000.

(Objects and thier corresponding Texts)

(Baba)
BabaPic
White
.000.
000.0
00000
.000.
.0.0.

Babatext
#fc3468
.00..
.0.0.
.00..
.0.0.
.00..

((Keke)
Keke
Red
.00..
.0.0.
00000
.0.0.
.0.0.

Keketext
Red
.0.0.
.0.0.
.00..
.0.0.
.0.0.)

(Skull)
SkullPic
Red
.000.
0.0.0
00000
.0.0.
.000.

Skulltext
Red
.000.
.0...
.000.
...0.
.000.

(Wall)
WallPic
darkblue darkgray
00010
11111
01000
11111
00010

Walltext
darkgray
0...0
0...0
0.0.0
0.0.0
.0.0.

(Water)
WaterPic
Blue darkgray
10010
00000
10000
00001
10100

Watertext
Blue
0...0
0...0
0.0.0
0.0.0
.0.0.

(Rock)
RockPic
orange yellow
.010.
01000
00101
01010
.000.

Rocktext
Brown
.00..
.0.0.
.00..
.0.0.
.0.0.

((Door)
DoorPic
brown gray yellow
.000.
00000
00110
00020
00000

Doortext
darkbrown
.00..
.0.0.
.0.0.
.0.0.
.00..)

(Flag)
FlagPic
yellow
..00.
..000
..0..
..0..
.000.

Flagtext
yellow
.000.
.0...
.000.
.0...
.0...

((Key)
KeyPic
yellow
.....
.0...
0.000
.0..0
.....

Keytext
yellow
.0.0.
.0.0.
.00..
.0.0.
.0.0.)


(Operators: is, and, [on, has, not])

(is)
is
White
0.00.
0.0..
0.00.
0..0.
0.00.

((and)
and
White
..0..
..0..
00000
..0..
..0..)


(Properties: You, Push, Stop,Goal, Sink, Defeat, Pull, Shut, Open [Tele, Melt])

YouText
black #fc3468 
10101
10101
10101
10101
10001

PushText
black Brown 
10011
10101
10011
10111
10111

StopText
black Green 
10001
10111
10001
11101
10001

SinkText
black Blue 
10001
10111
10001
11101
10001

GoalText
black yellow
01110
01110
01010
01010
10101

DefeatText
black Red
10011
10101
10101
10101
10011


(Pull
black orange
10011
10101
10011
10111
10111

Shut
black brown 
10001
10111
10001
11101
10001

Open
black Yellow 
11011
10101
10101
10101
11011)


(state)

BabaIsYou
transparent

RockIsYou
transparent

FlagIsYou
transparent

WallIsYou
transparent

WaterIsYou
transparent

SkullIsYou
transparent


BabaIsPush
transparent

RockIsPush
transparent

FlagIsPush
transparent

WallIsPush
transparent

WaterIsPush
transparent

SkullIsPush
transparent


BabaIsStop
transparent

RockIsStop
transparent

FlagIsStop
transparent

WallIsStop
transparent

WaterIsStop
transparent

SkullIsStop
transparent


BabaIsGoal
transparent

RockIsGoal
transparent

FlagIsGoal
transparent

WallIsGoal
transparent

WaterIsGoal
transparent

SkullIsGoal
transparent


BabaIsSink
transparent

RockIsSink
transparent

FlagIsSink
transparent

WallIsSink
transparent

WaterIsSink
transparent

SkullIsSink
transparent


BabaIsDefeat
transparent

RockIsDefeat
transparent

FlagIsDefeat
transparent

WallIsDefeat
transparent

WaterIsDefeat
transparent

SkullIsDefeat
transparent


=======
LEGEND
=======

(Player :HACK:: for taking input without an actual Player)
. = Background AND Player

(*** Objects & Texts ***)
(Unused Background Elements)
g = Grass
t = Tile
1 = Tree

BackgroundElements = g or t or 1

(Wall)
# = WallPic
Ŵ = Walltext

(Wall)
~ = WaterPic
ω = Watertext

(Baba)
B = BabaPic
Ɓ = Babatext

(Keke)
(K = Keke
ĸ = Keketext)

(Key)
(¬ = Key
Ķ = Keytext)

(Skull)
ౚ = SkullPic
Ș = Skulltext

(Door)
(Ƞ = Door
Đ = Doortext)

(Flag)
F = FlagPic
Ƒ = flagtext

(Rock)
R = RockPic
Ř = Rocktext

(************************)
(************************)

(****** Operators ******)
i = is
(a = and)

(****** Properties ******)
W =GoalText
P = PushText
Y = YouText
S = StopText
Ş = SinkText
D = DefeatText
(Ƨ = Shut
O = Open
Ƥ = Pull)

(****** Collections ******)
Baba  =  BabaIsYou OR  BabaIsPush OR  BabaIsStop OR  BabaIsGoal OR  BabaIsSink OR  BabaIsDefeat
Rock  =  RockIsYou OR  RockIsPush OR  RockIsStop OR  RockIsGoal OR  RockIsSink OR  RockIsDefeat
Flag  =  FlagIsYou OR  FlagIsPush OR  FlagIsStop OR  FlagIsGoal OR  FlagIsSink OR  FlagIsDefeat
Wall  =  WallIsYou OR  WallIsPush OR  WallIsStop OR  WallIsGoal OR  WallIsSink OR  WallIsDefeat
Water = WaterIsYou OR WaterIsPush OR WaterIsStop OR WaterIsGoal OR WaterIsSink OR WaterIsDefeat
Skull = SkullIsYou OR SkullIsPush OR SkullIsStop OR SkullIsGoal OR SkullIsSink OR SkullIsDefeat


You  = BabaIsYou  OR RockIsYou  OR FlagIsYou  OR WallIsYou  OR WaterIsYou  OR SkullIsYou
Push = BabaIsPush OR RockIsPush OR FlagIsPush OR WallIsPush OR WaterIsPush OR SkullIsPush
Stop = BabaIsStop OR RockIsStop OR FlagIsStop OR WallIsStop OR WaterIsStop OR SkullIsStop
Goal = BabaIsGoal OR RockIsGoal OR FlagIsGoal OR WallIsGoal OR WaterIsGoal OR SkullIsGoal
Sink = BabaIsSink OR RockIsSink OR FlagIsSink OR WallIsSink OR WaterIsSink OR SkullIsSink
Defeat = BabaIsDefeat OR RockIsDefeat OR FlagIsDefeat OR WallIsDefeat OR WaterIsDefeat OR SkullIsDefeat

Pics = BabaPic OR RockPic OR FlagPic OR WallPic OR WaterPic OR SkullPic (OR DoorPic OR KeyPic OR KekePic)
Object = Baba OR Rock OR Flag OR Wall OR Water OR Skull (OR Door OR Key OR Keke)

Property = Goal or Push or You or Stop or Sink or Defeat (or Shut or Open or Pull)

Operand = Flagtext or Rocktext or Walltext or Babatext or Watertext or Skulltext (or Doortext or Keketext or Keytext)

Operator = is (or and)


Text = Operator OR Operand OR GoalText OR PushText OR YouText OR StopText OR SinkText OR DefeatText

Movable = Text OR You OR Push



=======
SOUNDS
=======

Object MOVE 71305507

sfx0 22262508

sfx1 89546704

EndLevel 76428703

================
COLLISIONLAYERS
================

Player
Background
BackgroundElements

(
  Push
  Stop
  Sink
  Goal
  Defeat
  You

)


(Objects and draw order)
WaterPic
WallPic
(Door)
RockPic
SkullPic
FlagPic
(Key)

(Keke)
BabaPic


(Texts all collide!)
Text

(all the state sprites)
BabaIsYou
RockIsYou
FlagIsYou
WallIsYou
WaterIsYou
SkullIsYou
BabaIsPush
RockIsPush
FlagIsPush
WallIsPush
WaterIsPush
SkullIsPush
BabaIsStop
RockIsStop
FlagIsStop
WallIsStop
WaterIsStop
SkullIsStop
BabaIsGoal
RockIsGoal
FlagIsGoal
WallIsGoal
WaterIsGoal
SkullIsGoal
BabaIsSink
RockIsSink
FlagIsSink
WallIsSink
WaterIsSink
SkullIsSink
BabaIsDefeat
RockIsDefeat
FlagIsDefeat
WallIsDefeat
WaterIsDefeat
SkullIsDefeat

======
RULES
======

[ Property ] -> [ NO Property ]

[ BabaText | is |  YouText][ BabaPic NO  You] -> [ BabaText | is |  YouText] [ BabaIsYou]
[ RockText | is |  YouText][ RockPic NO  You] -> [ RockText | is |  YouText] [ RockIsYou]
[ FlagText | is |  YouText][ FlagPic NO  You] -> [ FlagText | is |  YouText] [ FlagIsYou]
[ WallText | is |  YouText][ WallPic NO  You] -> [ WallText | is |  YouText] [ WallIsYou]
[WaterText | is |  YouText][WaterPic NO  You] -> [WaterText | is |  YouText] [WaterIsYou]
[SkullText | is |  YouText][SkullPic NO  You] -> [SkullText | is |  YouText] [SkullIsYou]

[ BabaText | is | PushText][ BabaPic NO Push] -> [ BabaText | is | PushText] [ BabaIsPush]
[ RockText | is | PushText][ RockPic NO Push] -> [ RockText | is | PushText] [ RockIsPush]
[ FlagText | is | PushText][ FlagPic NO Push] -> [ FlagText | is | PushText] [ FlagIsPush]
[ WallText | is | PushText][ WallPic NO Push] -> [ WallText | is | PushText] [ WallIsPush]
[WaterText | is | PushText][WaterPic NO Push] -> [WaterText | is | PushText] [WaterIsPush]
[SkullText | is | PushText][SkullPic NO Push] -> [SkullText | is | PushText] [SkullIsPush]

[ BabaText | is | StopText][ BabaPic NO Stop] -> [ BabaText | is | StopText] [ BabaIsStop]
[ RockText | is | StopText][ RockPic NO Stop] -> [ RockText | is | StopText] [ RockIsStop]
[ FlagText | is | StopText][ FlagPic NO Stop] -> [ FlagText | is | StopText] [ FlagIsStop]
[ WallText | is | StopText][ WallPic NO Stop] -> [ WallText | is | StopText] [ WallIsStop]
[WaterText | is | StopText][WaterPic NO Stop] -> [WaterText | is | StopText] [WaterIsStop]
[SkullText | is | StopText][SkullPic NO Stop] -> [SkullText | is | StopText] [SkullIsStop]

[ BabaText | is | GoalText][ BabaPic NO Goal] -> [ BabaText | is | GoalText] [ BabaIsGoal]
[ RockText | is | GoalText][ RockPic NO Goal] -> [ RockText | is | GoalText] [ RockIsGoal]
[ FlagText | is | GoalText][ FlagPic NO Goal] -> [ FlagText | is | GoalText] [ FlagIsGoal]
[ WallText | is | GoalText][ WallPic NO Goal] -> [ WallText | is | GoalText] [ WallIsGoal]
[WaterText | is | GoalText][WaterPic NO Goal] -> [WaterText | is | GoalText] [WaterIsGoal]
[SkullText | is | GoalText][SkullPic NO Goal] -> [SkullText | is | GoalText] [SkullIsGoal]

[ BabaText | is | SinkText][ BabaPic NO Sink] -> [ BabaText | is | SinkText] [ BabaIsSink]
[ RockText | is | SinkText][ RockPic NO Sink] -> [ RockText | is | SinkText] [ RockIsSink]
[ FlagText | is | SinkText][ FlagPic NO Sink] -> [ FlagText | is | SinkText] [ FlagIsSink]
[ WallText | is | SinkText][ WallPic NO Sink] -> [ WallText | is | SinkText] [ WallIsSink]
[WaterText | is | SinkText][WaterPic NO Sink] -> [WaterText | is | SinkText] [WaterIsSink]
[SkullText | is | SinkText][SkullPic NO Sink] -> [SkullText | is | SinkText] [SkullIsSink]

[ BabaText | is | DefeatText][ BabaPic NO Defeat] -> [ BabaText | is | DefeatText] [ BabaIsDefeat]
[ RockText | is | DefeatText][ RockPic NO Defeat] -> [ RockText | is | DefeatText] [ RockIsDefeat]
[ FlagText | is | DefeatText][ FlagPic NO Defeat] -> [ FlagText | is | DefeatText] [ FlagIsDefeat]
[ WallText | is | DefeatText][ WallPic NO Defeat] -> [ WallText | is | DefeatText] [ WallIsDefeat]
[WaterText | is | DefeatText][WaterPic NO Defeat] -> [WaterText | is | DefeatText] [WaterIsDefeat]
[SkullText | is | DefeatText][SkullPic NO Defeat] -> [SkullText | is | DefeatText] [SkullIsDefeat]


[> Player] [You] -> [> Player] [> You]

(Simple rules)

[> Movable | STATIONARY Movable] -> [> Movable | > Movable]

[> You | Stop][You] -> [STATIONARY You | Stop][STATIONARY You] (stop all of you)

[> Stop] -> [STATIONARY Stop]


(Sink rules)
(Wall)
LATE [ WallIsSink  Text] -> [] SFX1
LATE [ WallIsSink  Baba] -> [] SFX1
LATE [ WallIsSink  Rock] -> [] SFX1
LATE [ WallIsSink  Flag] -> [] SFX1
LATE [ WallIsSink Water] -> [] SFX1
LATE [ WallIsSink Skull] -> [] SFX1

(Water)
LATE [WaterIsSink  Text] -> [] SFX1
LATE [WaterIsSink  Baba] -> [] SFX1
LATE [WaterIsSink  Rock] -> [] SFX1
LATE [WaterIsSink  Flag] -> [] SFX1
LATE [WaterIsSink  Wall] -> [] SFX1
LATE [WaterIsSink Skull] -> [] SFX1

(Baba)
LATE [ BabaIsSink  Text] -> [] SFX1
LATE [ BabaIsSink  Rock] -> [] SFX1
LATE [ BabaIsSink  Flag] -> [] SFX1
LATE [ BabaIsSink  Wall] -> [] SFX1
LATE [ BabaIsSink Water] -> [] SFX1
LATE [ BabaIsSink Skull] -> [] SFX1

(Rock)
LATE [ RockIsSink  Text] -> [] SFX1
LATE [ RockIsSink  Baba] -> [] SFX1
LATE [ RockIsSink  Flag] -> [] SFX1
LATE [ RockIsSink  Wall] -> [] SFX1
LATE [ RockIsSink Water] -> [] SFX1
LATE [ RockIsSink Skull] -> [] SFX1

(Flag)
LATE [ FlagIsSink  Text] -> [] SFX1
LATE [ FlagIsSink  Baba] -> [] SFX1
LATE [ FlagIsSink  Rock] -> [] SFX1
LATE [ FlagIsSink  Wall] -> [] SFX1
LATE [ FlagIsSink Water] -> [] SFX1
LATE [ FlagIsSink Skull] -> [] SFX1

(Skull)
LATE [SkullIsSink  Text] -> [] SFX1
LATE [SkullIsSink  Baba] -> [] SFX1
LATE [SkullIsSink  Flag] -> [] SFX1
LATE [SkullIsSink  Rock] -> [] SFX1
LATE [SkullIsSink  Wall] -> [] SFX1
LATE [SkullIsSink Water] -> [] SFX1




(Defeat rules)
(Wall)
LATE [ WallIsDefeat  BabaIsYou] -> [ WallIsDefeat] SFX1
LATE [ WallIsDefeat  RockIsYou] -> [ WallIsDefeat] SFX1
LATE [ WallIsDefeat  FlagIsYou] -> [ WallIsDefeat] SFX1
LATE [ WallIsDefeat WaterIsYou] -> [ WallIsDefeat] SFX1
LATE [ WallIsDefeat SkullIsYou] -> [ WallIsDefeat] SFX1

(Water)
LATE [WaterIsDefeat  BabaIsYou] -> [WaterIsDefeat] SFX1
LATE [WaterIsDefeat  RockIsYou] -> [WaterIsDefeat] SFX1
LATE [WaterIsDefeat  FlagIsYou] -> [WaterIsDefeat] SFX1
LATE [WaterIsDefeat  WallIsYou] -> [WaterIsDefeat] SFX1
LATE [WaterIsDefeat SkullIsYou] -> [WaterIsDefeat] SFX1

(Baba)
LATE [ BabaIsDefeat  RockIsYou] -> [ BabaIsDefeat] SFX1
LATE [ BabaIsDefeat  FlagIsYou] -> [ BabaIsDefeat] SFX1
LATE [ BabaIsDefeat  WallIsYou] -> [ BabaIsDefeat] SFX1
LATE [ BabaIsDefeat WaterIsYou] -> [ BabaIsDefeat] SFX1
LATE [ BabaIsDefeat SkullIsYou] -> [ BabaIsDefeat] SFX1

(Rock)
LATE [ RockIsDefeat  BabaIsYou] -> [ RockIsDefeat] SFX1
LATE [ RockIsDefeat  FlagIsYou] -> [ RockIsDefeat] SFX1
LATE [ RockIsDefeat  WallIsYou] -> [ RockIsDefeat] SFX1
LATE [ RockIsDefeat WaterIsYou] -> [ RockIsDefeat] SFX1
LATE [ RockIsDefeat SkullIsYou] -> [ RockIsDefeat] SFX1

(Flag)
LATE [ FlagIsDefeat  BabaIsYou] -> [ FlagIsDefeat] SFX1
LATE [ FlagIsDefeat  RockIsYou] -> [ FlagIsDefeat] SFX1
LATE [ FlagIsDefeat  WallIsYou] -> [ FlagIsDefeat] SFX1
LATE [ FlagIsDefeat WaterIsYou] -> [ FlagIsDefeat] SFX1
LATE [ FlagIsDefeat SkullIsYou] -> [ FlagIsDefeat] SFX1

(Skull)
LATE [SkullIsDefeat  BabaIsYou] -> [SkullIsDefeat] SFX1
LATE [SkullIsDefeat  FlagIsYou] -> [SkullIsDefeat] SFX1
LATE [SkullIsDefeat  RockIsYou] -> [SkullIsDefeat] SFX1
LATE [SkullIsDefeat  WallIsYou] -> [SkullIsDefeat] SFX1
LATE [SkullIsDefeat WaterIsYou] -> [SkullIsDefeat] SFX1




LATE [You Goal] -> WIN

(restore the pics because we are done with logic)

LATE [ Baba] -> [ Baba  BabaPic]
LATE [ Rock] -> [ Rock  RockPic]
LATE [ Flag] -> [ Flag  FlagPic]
LATE [ Wall] -> [ Wall  WallPic]
LATE [Water] -> [Water WaterPic]
LATE [Skull] -> [Skull SkullPic]

LATE [Property] -> []

==============
WINCONDITIONS
==============

(Win condition is determined in the RULES)

=======
LEVELS
=======

message BABA IS YOU (DEMAKE)

.................
.ƁiY.........ƑiW.
.................
....#########....
........R........
....B...R...F....
........R........
....#########....
.................
.ŴiS.........ŘiP.
.................

message WHERE DO I GO?

...................................
.......g...........................
...........#############...........
...........#...........#........g..
...........#..i........#...........
...........#...........#...........
...........#.......W...#.g.........
......######...........#..g........
.g....#ttttt...........#...........
......#tƑttt...F.......#.....g.....
..g...#ttttt...........#...........
......##################...........
...........#...........#...........
.......Ɓ...#.Ŵ.........#...........
.......i...#.i.....B...#.......g...
.......Y...#.S.........#......gg...
...........#...........#...........
...........#############.....g.....
..g................................
...................................


message NOW WHAT IS THIS?

...................................
.......t...........................
...........FFFFFFFFFFFFF...........
...........F...........F........t..
...........F..i........F...........
...........F...........F...........
...........F.......W...F.t.........
......FFFFFF...........F..t........
.t....Fggggg...........F...........
......FgƁggg...........F.....t.....
..t...Fggggg...........F...........
......FFFFFFFFFFFFFFFFFF...........
...........F...........F...........
.......Ŵ...F.Ƒ.........F...........
.......i...F.i.....#...F.......t...
.......Y...F.S.........F......tt...
...........F...........F...........
...........FFFFFFFFFFFFF.....t.....
..t................................
...................................


message OUT OF REACH

ƁŴ#................................
ii#....g...................g.......
YS#........#############...........
###........#ttttttttttt#........g..
..........ω#ttBtttttRtt#...........
..........i#ttttttttttt#...........
..........Ş#ttttttttRtt#.g.........
......######~~~~############.......
.g....#.........#..........#....g..
......#.........#..........#.......
..g...#.........#....ŘiP...#.......
......#.........#..........#.......
......#.........#..........#.......
......#~~~~...#............#.......
......#~~~~.....#..........#...g...
......#~~~~.....#....ƑiW...#..gg...
......#F~~~.....#..........#.......
......######################.g.....
..g................................
...................................


message STILL OUT OF REACH

ƑiW.........................
ƁiY.............ౚౚౚౚౚౚౚౚౚ...
................ౚ.Ș.....ౚ...
................ౚ.i.....ౚ...
................ౚ.D...F.ౚ...
..ŘiP...........ౚ.......ౚ...
................ౚౚౚౚౚౚౚౚౚ...
.....ౚ.ౚ....................
.....ౚRౚ....................
...ౚౚౚRౚౚౚ..................
...ౚ..R..ౚ..................
...ౚ.....ౚ..................
...ౚ.....ౚ..................
...ౚ..B..ౚ..................
...ౚ.....ౚ..................

message DEEP RIVER (aka VOLCANO, Hot/Melt not implemented)

ŴiS~~.#.....#...#.........~~~~~~..
~~~~..#.......B.#........~~~~~....
~~....#.....#...#.......~~~~~.....
~.....#.ƁiY.#####......~~~~~~.....
......##...##..........~~~~~......
......#.....R.........~~~~~~......
......#######........~~~~~~.......
......#Ř#...........~~~~~~........
.......i...........~~~~~~.........
.......P..........~~~~~~..........
............ω....~~~~~~...........
................~~~~~~......F.....
.......#ωiŞ#...~~~~~~............~
.......#ƑiD#..~~~~~~.....ƑiW....~~
.......#####.~~~~~~............~~~

message OFF LIMITS

ŘiS.R.......ౚ.....................
....R.......ౚ.....................
ȘiD.R.....##ౚ########.............
....R.....#.ౚ..#....#.............
ƑiW.R.....#.ౚ.....F.#.............
....R.#####.ౚ..#....#.............
RRRRR.#...#.ౚ..######.............
......#.B...ౚ..#..................
......#...#.ౚ###..................
......#####.ౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚౚ
..........#....#..................
..........#.ŴiS#..................
.......Ɓ..#....#..................
.......i..######..................
.......y..........................
..................................


message ROCK WORLD (Grass World)

.............................
.......################......
.......#....R.........#......
.......#.ƁiY........F.#......
.......#R.......R.....#......
.......#.........R.R..#.....Ř
.......#...B...R..R.RR#.....i
.......#R......R.Ƒ.W..#.....S
.......#.R.....R..R...#......
.......#....R...R.....#......
.......################......
.............................
.............................

message BRICK WALL

...g.............
.............g...
..gttttt...g.....
.g.tƁiYt...######
...ttttt...#...#Ŵ
.B.ttttt...#.F.#i
...ttttt...#...#S
...tƑiWt...######
...ttttt.........
.............g...
....g............


message Watery Grave (by @pyjamads)

.......................
...................~~~~
..ƁiY.........ౚౚౚౚౚ~șωŵ
..............ౚƒiWౚ~iii
..............ౚౚౚౚౚ~DşS
...................~~~~
.......................
.######.....r..........
##....#.##.............
#..řiPR..#.............
#.B...#.##.........B...
##....#....~~~.........
.#####....~~~~~........
..........~~F~~........
..........~~~~~........
...........~~~.........
.......................

(HAVEN'T Implemented Key/Door + Open/Shut fully
message LOCK

........................#ĐiS#.g...
......#####....g........#####..g..
......#ƁiY#.............#ĐiƧ#...g.
.g....#####............#######....
......#ŴiS#............#.....#....
......#######...tttttttȠ..Ř..#....
...g..#.....#...tR.¬...#.....#....
......#.¬.B.Ƞtttt......###Ƞ###....
......#.....#...t......#....#.....
......#######...t......#..F.#.....
................t......##...#.....
.1........ĶiO...t.......#ƑiW#.....
..........i.....tttt....#####.....
..1..g....P........t..............
....g..............t..............
)

message THANK YOU FOR PLAYING! GO BUY BABA IS YOU!